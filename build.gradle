buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

apply plugin: 'java'
apply plugin: 'idea'

version = property('version')
group = property('group')
archivesBaseName = property('id')

// Include data-generated resources in the build and run processes
sourceSets {
    test.resources.srcDirs += 'src/test/generated/resources'
}

// Setup the minecraft modding environment
minecraft {
    // Set the mappings used to translate the minecraft compiled code to readable code.
    mappings channel: 'snapshot', version: property('mcp.version')

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Set the run configurations
    runs {
        client {
            property 'forge.logging.console.level', 'debug'
            if (findProject(":workspace") != null) {
                ideaModule "${rootProject.name}.${project.name}.main"
            } else {
                ideaModule "${project.name}.main"
            }
            property 'fml.earlyprogresswindow', 'false'
            workingDirectory project.file('run')
            args '--username', property('mc.username'), '--password', property('mc.password')
            mods {
                Mod {
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }

        server {
            property 'forge.logging.console.level', 'debug'
            if (findProject(":workspace") != null) {
                ideaModule "${rootProject.name}.${project.name}.main"
            } else {
                ideaModule "${project.name}.main"
            }
            workingDirectory project.file('run')
            mods {
                Mod {
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }

        data {
            property 'forge.logging.console.level', 'debug'
            if (findProject(":workspace") != null) {
                ideaModule "${rootProject.name}.${project.name}.main"
            } else {
                ideaModule "${project.name}.main"
            }
            property 'fml.earlyprogresswindow', 'false'
            workingDirectory project.file('run')
            args '--mod', property('id'),
                    '--all',
                    '--output', file('src/test/generated/resources/'),
                    '--existing', file('src/test/resources'),
                    '--existing', file('src/test/generated/resources/')
            mods {
                Mod {
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }
    }
}

// Add shade the the configs
configurations {
    shade
}

// Include maven repos for required projects
repositories {
    maven {
        name = "Progwml6 maven"  // JEI
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {  // Registrate
        name = "tterrag maven"
        url = "https://maven.tterrag.com/"
    }
}

// Add dependancies
dependencies {
    minecraft "net.minecraftforge:forge:${property('mc.version')}-${property('forge.version')}"

    def registrate = "com.tterrag.registrate:Registrate:${property('registrate.version')}"
    implementation fg.deobf(registrate) {
        transitive = false
    }
    shade registrate

    compileOnly fg.deobf("mezz.jei:jei-${property('jei.version')}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${property('jei.version')}")

    implementation 'com.intellij:annotations:+@jar'
}

// Shadow registrate into the jar
shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', "${property("group")}.registrate"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.property('id'),
                "Specification-Vendor"    : project.property('authors'),
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : project.property('group'),
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}